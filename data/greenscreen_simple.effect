uniform float4x4 ViewProj;
uniform texture2d image;

uniform float4 key_color;
uniform float similarity;
uniform float desaturation;
uniform float darkness;
uniform	float radius;

sampler_state textureSampler {
	Filter    = Linear;
	AddressU  = Clamp;
	AddressV  = Clamp;
};

struct VertData {
	float4 pos : POSITION;
	float2 uv  : TEXCOORD0;
};

VertData VSDefault(VertData v_in)
{
	VertData vert_out;
	vert_out.pos = mul(float4(v_in.pos.xyz, 1.0), ViewProj);
	vert_out.uv  = v_in.uv;
	return vert_out;
}

float3 RgbToHsv(float3 c)
{
	float3 hsv = 0;
	float maxc = max(max(c.r, c.g), c.b);
	float minc = min(min(c.r, c.g), c.b);
	
	hsv[2] = maxc;
	if (minc == maxc)
		return hsv;
	
	float diff = maxc - minc;
	if (maxc != 0)
		hsv[1] = diff / maxc;
	
	float h = 0;
	if (c.r == maxc)
		h = 1.0f + (c.g - c.b) / diff;
	else if (c.g == maxc)
		h = 3.0f + (c.b - c.r) / diff;
	else
		h = 5.0f + (c.r - c.g) / diff;
	  
	hsv[0] = h / 6.0f;

	return hsv;
}

float GetMask(float3 hsv, float3 key)
{
	float mask = 0;

	// hue distance
	float hueDelta = min(abs(hsv[0] - key[0]), abs(hsv[0] - key[0] - 1));
	if (hueDelta >= similarity)
		mask = 1;
	
	// keep if desaturated
	if (hsv[1] < desaturation)
		mask = 1;
		
	// keep if dark
	if (hsv[2] < darkness)
		mask = 1;

	return mask;
}

float4 SampleColor(float2 uv)
{
	float4 rgba = image.Sample(textureSampler, uv);
	rgba.rgb *= (rgba.a > 0.) ? (1. / rgba.a) : 0.;
	return rgba;
}

float4 PSColorKeyRGBA(VertData v_in) : TARGET
{
	float3 key = RgbToHsv(key_color.rgb);
	float4 color = SampleColor(v_in.uv);
	
#define NUM_SAMPLES 8
#define NUM_TOLERANCE 3
	float2 positions[NUM_SAMPLES];
	positions[0] = float2(v_in.uv.x + radius, v_in.uv.y);
	positions[1] = float2(v_in.uv.x - radius, v_in.uv.y);
	positions[2] = float2(v_in.uv.x, v_in.uv.y - radius);
	positions[3] = float2(v_in.uv.x, v_in.uv.y + radius);
	positions[4] = float2(v_in.uv.x - radius * .707f, v_in.uv.y - radius * .707f);
	positions[5] = float2(v_in.uv.x - radius * .707f, v_in.uv.y + radius * .707f);
	positions[6] = float2(v_in.uv.x + radius * .707f, v_in.uv.y - radius * .707f);
	positions[7] = float2(v_in.uv.x + radius * .707f, v_in.uv.y + radius * .707f);
	
	int countFore = 0;
	int countBack = 0;
	float4 colorFore = 0;
	float4 colorBack = 0;
	for (int i = 0; i < NUM_SAMPLES; i++)
	{
		float4 c = SampleColor(positions[i]);
		float m = GetMask(RgbToHsv(c.rgb), key);
		if (m > .5f)
		{
			countFore++;
			colorFore += c;
		}
		else
		{
			countBack++;
			colorBack += c;
		}
	}
	colorFore /= countFore;
	colorBack /= countBack;
	
	float4 result = color;
	
	float mask = GetMask(RgbToHsv(color.rgb), key);
	if (mask < .5f)
	{
		if (countFore >= NUM_SAMPLES - NUM_TOLERANCE)
			mask = 1;
	}
	else
	{
		if (countFore <= NUM_TOLERANCE)
		{
			mask = 0;
		}
		else if (countFore < NUM_SAMPLES - NUM_TOLERANCE)
		{
			result.rgb = colorFore.rgb;
			mask = float(countFore) / NUM_SAMPLES;
		}
	}
	
	result.a *= mask;
	result.rgb *= result.a;
	return result;
}

technique Draw
{
	pass
	{
		vertex_shader = VSDefault(v_in);
		pixel_shader  = PSColorKeyRGBA(v_in);
	}
}
